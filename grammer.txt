program        = declaration* EOF ;

declaration    = classDecl
                 | funDecl
                 | varDecl
                 | statement ;

classDecl      = "class" IDENTIFIER ( "<" IDENTIFIER )? "{" function* "}" ;
funDecl        = "fun" function ;
varDecl        = "var" IDENTIFIER ( "=" expression )? ";" ;
statement      = exprStmt
                 | forStmt
                 | ifStmt
                 | printStmt
                 | returnStmt
                 | whileStmt
                 | block ;

function       = IDENTIFIER "(" parameters? ")" block ;
exprStmt       = expression ";" ;
forStmt        = "for" "(" ( varDecl | exprStmt | ";" ) expression? ";" expression? ")" statements ;
ifStmt         = "if" "(" expression ")" statement ( "else" statement )? ;
printStmt      = "print" expression ";" ;
returnStmt     = "return" expression? ";" ;
whileStmt      = "while" "(" expression ")" statement ;
block          = "{" declaration* "}" ;

parameters     = IDENTIFIER ( "," IDENTIFIER )* ;
expression     = assignment ;
assignment     = ( call "." )? IDENTIFIER "=" assignment
                 | logic_or ;
logic_or       = logic_and ( "or" logic_and )* ;
logic_and      = equality ( "and" equality )* ;
equality       = comparison ( ( "!=" | "==" ) comparison )* ;
comparison     = addition ( ( ">" | ">=" | "<" | "<=" ) addition )* ;
addition       = multiplication ( ( "+" | "-" ) multiplication )* ;
multiplication = unary ( ( "/" | "*" ) unary )* ;
unary          = ( "!" | "-" ) unary
                 | call ;
call           = primary ( "(" arguments? ")" | "." IDENTIFIER )* ;
arguments      = expression ( "," expression )* ;
primary        = NUMBER
                 | STRING
                 | THIS
                 | "false"
                 | "true"
                 | "nil"
                 | "(" expression ")"
                 | IDENTIFIER
                 | SUPER "." IDENTIFIER ;
