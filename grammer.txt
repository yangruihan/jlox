program        = declaration* EOF ;

declaration    = varDecl | statement ;

varDecl        = "var" IDENTIFIER ( "=" expression )? ";" ;
statement      = exprStmt | printStmt | block ;

exprStmt       = expression ";" ;
printStmt      = "print" expression ";" ;
block          = "{" declaration* "}" ;

expression     = assignment ;
assignment     = IDENTIFIER "=" assignment | equality ;
equality       = comparison ( ( "!=" | "==" ) comparison )* ;
comparison     = addition ( ( ">" | ">=" | "<" | "<=" ) addition )* ;
addition       = multiplication ( ( "+" | "-" ) multiplication )* ;
multiplication = unary ( ( "/" | "*" ) unary )* ;
unary          = ( "!" | "-" ) unary
                 | primary ;
primary        = NUMBER | STRING | "false" | "true" | "nil"
                 | "(" expression ")"
                 | IDENTIFIER ;
